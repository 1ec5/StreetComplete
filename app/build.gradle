apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    signingConfigs {
        release
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "de.westnordost.streetcomplete"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "0.4"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(keystorePropertiesFile))
    android.signingConfigs.release.storeFile = file(props['storeFile'])
    android.signingConfigs.release.storePassword = props['storePassword']
    android.signingConfigs.release.keyAlias = props['keyAlias']
    android.signingConfigs.release.keyPassword = props['keyPassword']
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // tests (perhaps migrate to junit4 later if any features of it seem convenient or necessary)
    testCompile 'junit:junit:3.8.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.objenesis:objenesis:2.2'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.objenesis:objenesis:2.2'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"


    // dependency injection
    compile 'com.google.dagger:dagger:2.5'
    apt 'com.google.dagger:dagger-compiler:2.5'

    // Android stuff
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:support-v13:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:design:25.2.0'



    // talking with OSM Api
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile ('de.westnordost:osmapi:1.4') {
        // it's already included in Android
        exclude group: 'net.sf.kxml', module: 'kxml2'
    }

    // serialization
    compile 'com.esotericsoftware:kryo:4.0.0'

    // map and location
    compile 'com.mapzen.tangram:tangram:0.4.9'
    compile 'com.mapzen.android:lost:2.2.0'

    // config files
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.09'

    compile 'com.google.guava:guava:20.0'

}
